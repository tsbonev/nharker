repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

buildscript {
    ext.kotlinVersion = "1.2.71"

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {

    apply plugin: "jacoco"

    jacoco {
        toolVersion = "0.8.1"
    }

    group 'com.tsbonev.nharker'
    version '0.2'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8

    test {
        jacoco {
            append = false
            destinationFile = file("$buildDir/jacoco/moduleTestsCoverage.exec")
            includeNoLocationClasses = true
            excludes = ['jdk.internal.*']
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.jmock', name: 'jmock-junit4', version: '2.8.4'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}